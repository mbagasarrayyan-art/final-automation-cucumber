plugins {
    id 'java'
}

group = 'com.bagas'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:5.10.2")
    testImplementation "org.junit.jupiter:junit-jupiter"

    // Cucumber (JUnit Platform Engine)
    testImplementation "io.cucumber:cucumber-java:7.18.1"
    testImplementation "io.cucumber:cucumber-junit-platform-engine:7.18.1"

    // JUnit Platform Suite API (buat @Suite, @SelectClasspathResource, dll)
    testImplementation "org.junit.platform:junit-platform-suite:1.10.2"

    // Selenium
    testImplementation "org.seleniumhq.selenium:selenium-java:4.20.0"


    testImplementation "io.github.bonigarcia:webdrivermanager:5.7.0"

    // Rest Assured
    testImplementation "io.rest-assured:rest-assured:5.4.0"
    testImplementation "io.rest-assured:json-path:5.4.0"
    testImplementation "io.rest-assured:xml-path:5.4.0"

    // Jackson
    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.17.1"

    // Optional
    testImplementation "org.hamcrest:hamcrest:2.2"
}

test {
    useJUnitPlatform()


    exclude("**/ApiTestRunner*")
    exclude("**/WebTestRunner*")
}

tasks.register('apiTest', Test) {
    group = "verification"
    description = "Run Cucumber API tests only"

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()


    setTestNameIncludePatterns([
            "com.bagas.finalproject.api.runner.ApiTestRunner"
    ])

    testLogging {
        events "passed", "failed", "skipped"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

tasks.register('webTest', Test) {
    group = "verification"
    description = "Run Cucumber Web UI tests only"

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()


    setTestNameIncludePatterns([
            "com.bagas.finalproject.web.runner.WebTestRunner"
    ])

    testLogging {
        events "passed", "failed", "skipped"
        exceptionFormat "full"
        showStandardStreams = true
    }

    environment "HEADLESS", System.getenv("HEADLESS") ?: "true"
}


