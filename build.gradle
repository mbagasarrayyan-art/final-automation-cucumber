plugins {
    id 'java'
}

group = 'com.bagas'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    // JUnit 5
    testImplementation platform("org.junit:junit-bom:5.10.2")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    // Cucumber (JUnit Platform Engine)
    testImplementation "io.cucumber:cucumber-java:7.14.0"
    testImplementation "io.cucumber:cucumber-junit-platform-engine:7.14.0"



    testImplementation "org.junit.platform:junit-platform-suite:1.10.2"

    // Selenium + WebDriverManager
    testImplementation "org.seleniumhq.selenium:selenium-java:4.18.1"
    testImplementation "io.github.bonigarcia:webdrivermanager:5.7.0"

    // Rest Assured
    testImplementation "io.rest-assured:rest-assured:5.4.0"

    // Assertion
    testImplementation "org.hamcrest:hamcrest:2.2"
}

test {
    useJUnitPlatform()
}


tasks.register("apiTest", Test) {
    useJUnitPlatform()

    systemProperty "cucumber.filter.tags", "@api"

    systemProperty "cucumber.plugin",
            "pretty, html:build/reports/cucumber/api.html, json:build/reports/cucumber/api.json"
    testLogging {
        events "passed", "skipped", "failed"
    }
}


tasks.register("webTest", Test) {
    useJUnitPlatform()
    systemProperty "cucumber.filter.tags", "@web"
    systemProperty "cucumber.plugin",
            "pretty, html:build/reports/cucumber/web.html, json:build/reports/cucumber/web.json"
    testLogging {
        events "passed", "skipped", "failed"
    }
}
